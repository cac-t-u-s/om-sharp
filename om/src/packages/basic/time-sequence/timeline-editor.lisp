;============================================================================
; om7: visual programming language for computer-aided music composition
; Copyright (c) 2013-2017 J. Bresson et al., IRCAM.
; - based on OpenMusic (c) IRCAM 1997-2017 by G. Assayag, C. Agon, J. Bresson
;============================================================================
;
;   This program is free software. For information on usage 
;   and redistribution, see the "LICENSE" file in this distribution.
;
;   This program is distributed in the hope that it will be useful,
;   but WITHOUT ANY WARRANTY; without even the implied warranty of
;   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. 
;
;============================================================================
; File author: J. Garcia
;============================================================================

(in-package :om)

;graphical vars
;this define the view height and the items size as well (70% height)
(defparameter *timeline-view-height* 20)
(defparameter *timeline-item-height* (* *timeline-view-height* 0.7))


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; TIMELINE EDITOR
;;; IS GENERALLY ATTACHED TO ANOTHER EDITOR

(defclass timeline-editor (OMEditor play-editor-mixin) 
  ((time-ruler :accessor time-ruler :initform nil :initarg :time-ruler)
   (timeline-views :accessor timeline-views :initform nil :initarg :timeline-views)
   (snap-to-grid :accessor snap-to-grid :initform t :initarg :snap-to-grid)))


(defmethod editor-get-time-sequence ((self OMEditor) id) 
  (declare (ignore id))
  (object-value self))

(defmethod editor-get-time-sequence ((self timeline-editor) id)
  (editor-get-time-sequence (container-editor self) id))

(defmethod editor-get-all-time-sequences ((self OMEditor)) 
  (list (object-value self)))


(defmethod delete-editor-selection ((self t)) nil)

(defmethod editor-delete-contents-from-timeline ((self OMEditor) timeline-id objects-id)
  ;;; we suppose that the editor's selection is in sync with the timeline
  (delete-editor-selection self))

(defmethod cursor-panes ((self timeline-editor))
  (when (and (timeline-views self) (time-ruler self))
    (append (list (time-ruler self)) (timeline-views self))))

(defmethod set-cursor-time ((self timeline-editor) time)
  (mapcar #'(lambda (pane) (update-cursor pane time)) (cursor-panes self))
  (set-cursor-time (container-editor self) time)
  (set-time-display self time)
  (om-invalidate-view (time-ruler self)))

(defmethod get-cursor-time ((self timeline-editor))
  (if (time-ruler self) (cursor-pos (time-ruler self)) 0))

(defmethod update-to-editor ((self timeline-editor) (from omeditor))
  (let ((obj (editor-get-time-sequence from nil))
        (sel (selection from)))
    (when obj 
      (let ((sel-points (if (find T sel)
                            (time-sequence-get-timed-item-list obj)
                          (get-points-from-indices obj sel))))
        (setf (selection self) sel-points))
      (editor-invalidate-views self))))

;; called from the time ruler
(defmethod update-to-editor ((self timeline-editor) (from time-ruler))
  (update-to-editor (container-editor self) self))

(defmethod update-to-editor ((self timeline-editor) (from t))
  (editor-invalidate-views self))


; used to update selection and value in the timeline editor
; !! work only if the editor has a slot 'timeline-editor !!
(defmethod update-timeline-editor ((self OMeditor)) 
  (when (and (timeline-editor self) (window self))
    (update-to-editor (timeline-editor self) self)
    t))

(defmethod update-to-editor ((editor OMEditor) (from timeline-editor))
  (let ((time-sequence (editor-get-time-sequence editor nil)))
    (when time-sequence 
      (setf (selection editor) (get-indices-from-points time-sequence (selection from)))
      (time-sequence-update-internal-times time-sequence))
    ;(update-timeline-editor editor)
    (report-modifications editor)))


(defmethod editor-invalidate-views ((self timeline-editor))
  (om-invalidate-view (get-g-component self :main-panel)))


;;;;;;;; PIVOTS TEMPORELS;;;;;;;;;;;;
(defmethod snap-all-points-to-grid ((self timeline-editor) id &optional (snap-delta nil))
  (let* ((obj (editor-get-time-sequence self id)))
    (loop for point in (time-sequence-get-timed-item-list obj)
          for time in (time-sequence-get-internal-times obj)
          do
          (item-set-time point (snap-time-to-grid (time-ruler self) time))
          )))

(defmethod snap-point-to-grid ((self timeline-editor) id pos &optional (snap-delta nil))
  (let* ((unit-dur (get-units (time-ruler self)))
         (delta (if snap-delta (min snap-delta (/ unit-dur 2)) (/ unit-dur 2)))
         (point (nth pos (time-sequence-get-timed-item-list (editor-get-time-sequence self id)))))
    (item-set-time point (snap-time-to-grid (time-ruler self) (item-get-time point) delta))
  ))


(defmethod play-editor-get-ruler-views ((self timeline-editor)) (time-ruler self))

(defmethod reinit-ranges ((self timeline-editor))
  (reinit-x-ranges self))


;;;==========================
;;; TIMELINE VIEW
;;;==========================

(defclass om-timeline-view (x-cursor-graduated-view OMEditorView)
  ((selected-p :accessor selected-p :initform nil)
   (label :accessor label :initform nil :initarg :label)
   (id :accessor id :initarg :id :initform -1))
  (:default-initargs :visible-min-height *timeline-view-height*)) 

;;;==========================
;;; selection
(defmethod select-timeline ((self om-timeline-view) t-or-nil)
  (setf (selected-p self) t-or-nil)
  (when (> (length (timeline-views (editor self))) 1)
    (om-set-bg-color self 
                     (if t-or-nil 
                         (om-get-light-offset-color (get-color (editor-get-time-sequence (editor self) (id self))) 0.8) 
                       (om-def-color :white)))
    (om-invalidate-view self))
  t-or-nil)

(defmethod get-selected-timelines ((self timeline-editor))
  (loop for tlv in (timeline-views self) 
        for i = 0 then (+ i 1)
        when (selected-p tlv)
        collect i))

(defmethod set-selected-timelines ((self timeline-editor) list-of-i)
  (loop for tlv in (timeline-views self) 
        for i = 0 then (+ i 1) do
        (select-timeline 
         tlv
         (if (find i list-of-i :test '=) t nil))))
;;;==========================



(defmethod update-view-from-ruler ((rv x-ruler-view) (view om-timeline-view))
  (setf (x1 view) (/ (v1 rv) (expt 10 (decimals rv))) 
        (x2 view) (/ (v2 rv) (expt 10 (decimals rv))))
  (set-shift-and-factor view)
  (call-next-method)
  (om-invalidate-view view))

(defmethod initialize-instance :after ((self om-timeline-view) &rest args) 
  (om-set-bg-color self (om-def-color :transparent))
  (unless (label self) (setf (label self) ""))
  (start-cursor self)  ;add a cursor directly
  )

;do not remove the cursor from a timeline view
(defmethod stop-cursor ((self om-timeline-view)) nil)

(defmethod get-obj-to-play ((self timeline-editor)) 
  (get-obj-to-play (container-editor self)))

(defmethod get-color (self) (om-def-color :dark-gray))


(defmethod clear-timeline ((self timeline-editor))
  (let ((container-layout (get-g-component self :main-panel)))
    (when (car (om-subviews container-layout)) ;;I need this to remove the button icon. It should be removed from the layout that contains it. Ratios need to be adapted before..
      (om-set-layout-ratios (car (om-subviews (car (om-subviews container-layout)))) nil)
      (om-remove-all-subviews (car (om-subviews (car (om-subviews container-layout))))))
    (om-remove-all-subviews container-layout)
    (setf (time-ruler self) nil)))

; a view at the left of the timeline.
; should be defined and sized depending on the container editor's layout
(defmethod make-timeline-left-item ((self t) id) (om-make-view 'om-view :size (omp 15 15)))

(defmethod build-transport-view ((self OMEditor))
  (om-make-layout 'om-row-layout :subviews (list (make-time-monitor (timeline-editor self) :time 0))))

(defmethod build-options-view ((self timeline-editor))
  (let ((snap-to-grid-chk (om-make-di 'om-check-box :text "Snap to Grid" :size (omp 100 24) :font (om-def-font :font1)
                                       :checked-p (snap-to-grid self)
                                       :di-action #'(lambda (item) 
                                                      (setf (snap-to-grid self) (om-checked-p item)
                                                            (snap-to-grid (time-ruler self)) (om-checked-p item))
                                                      (editor-invalidate-views self)))))
    (om-make-layout 'om-row-layout :subviews (list snap-to-grid-chk))))

(defmethod build-transport-and-options-layout ((self timeline-editor))
  (let* ((transport-layout (build-transport-view (container-editor self)))
         (options-layout (build-options-view self)))
    (om-make-layout 'om-row-layout
                    :subviews
                    (list 
                       (om-make-view 'om-view :direct-draw nil
                                     :subviews (list transport-layout))
                       nil
                       (om-make-view 'om-view :direct-draw nil
                                     :subviews (list options-layout )))
                    :ratios '(0.1 100 0.1)
                    )))

(defmethod make-timeline-view ((self timeline-editor))
  (let* ((container-editor (container-editor self))
         (main-panel (get-g-component self :main-panel))
         (time-ruler (om-make-view 'time-ruler  :size (omp nil 20) 
                                   :unit :ms :bg-color (om-def-color :white) :bottom-p nil 
                                   :snap-to-grid (snap-to-grid self) :onset-p nil))
         (timeline-views nil)
         (left-item-w 0)
         (foldable-containers nil))
    (loop for obj in (editor-get-all-time-sequences container-editor) 
          for i = 0 then (+ i 1) do
          (let* ((timeline-view (om-make-view 'om-timeline-view :id i :editor self :bg-color (om-def-color :white)))
                 (foldable-container (om-make-layout 'om-column-layout))
                 (timeline-item (make-timeline-left-item container-editor (id timeline-view)))
                 (fold-icon (om-make-graphic-object 
                             'om-icon-button :size (omp 10 10)
                             :icon :arrow-drop-right :icon-pushed :arrow-drop-up
                             :lock-push t
                             :action #'(lambda (b)
                                         (if (pushed b)
                                             (let ((extra-views (get-timeline-foldable-views container-editor :obj obj :time-ruler time-ruler)))
                                               (apply 'om-add-subviews (cons foldable-container extra-views))
                                               (reinit-ranges self))
                                           (progn
                                             (om-remove-all-subviews foldable-container)
                                             (setf (related-views time-ruler) (timeline-views self))))
                                         (om-update-layout (main-view (container-editor self)))
                                         (om-invalidate-view main-panel))))
                 (fold-group (om-make-layout 'om-column-layout 
                                             :ratios '(1.0 0.001)
                                             :subviews
                                             (list
                                              (om-make-layout 
                                               'om-row-layout
                                               :ratios '(0.001 1)
                                               :subviews
                                               (list timeline-item timeline-view))
                                               ;(list timeline-item fold-icon timeline-view)) ;; (om-make-view 'om-view :size (omp 10 10))
                                              foldable-container))))
            (setq left-item-w (om-width timeline-item)) 
            (pushr timeline-view timeline-views)
            (pushr fold-group foldable-containers)))
    
    (setf (related-views time-ruler) (append timeline-views (related-views time-ruler)))
    (om-remove-all-subviews main-panel)
    (when main-panel
      (om-add-subviews main-panel (om-make-layout 
                                   'om-column-layout 
                                   :size (omp 500 50)
                                   :delta 0
                                   :subviews
                                   (append
                                    (list (build-transport-and-options-layout self))
                                    (append  
                                     foldable-containers
                                     (list (om-make-layout 
                                            'om-row-layout
                                            :subviews
                                            (list
                                             ;;; a dummy view to take the same size as the timeline-item
                                             (om-make-view 'om-view :size (omp left-item-w nil)) 
                                             time-ruler)
                                            :ratios '(0.001 1)))))))
      (setf (time-ruler self) time-ruler)
      (setf (timeline-views self) timeline-views)
      (reinit-ranges self)
      (om-invalidate-view main-panel)
      )))


(defclass om-timeline-subview (om-view x-graduated-view) ())

;to redefine by each subclass
(defmethod get-timeline-foldable-views ((self omeditor) &key obj time-ruler)
  (list (om-make-view 'om-timeline-subview :bg-color (om-def-color :gold) :size (omp nil 40))))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;; DRAWING

(defmethod draw-timeline-background ((self OMEditor) view id) nil)

(defmethod om-draw-contents ((self om-timeline-view))
  (let* ((editor (editor self))
         (obj (editor-get-time-sequence (container-editor editor) (id self))))
    
    (draw-timeline-background (container-editor editor) self (id self))
    (om-with-fg-color (om-make-color 0.4 0.4 0.7 0.6)
      (when (time-ruler editor)
        (draw-grid-from-ruler self (time-ruler editor))))
    
    ;; x-cursor-graduate-view : draw interval etc.
    (call-next-method)
    
    (when obj
      (let* ((color (get-color obj))
             (x1 0)
             (x2 (om-point-x (om-view-size self)))
             (y_max (om-point-y (om-view-size self)))
             (y (/ y_max 2))
             (name (format nil "狺扉篝灬忮箦戽┅┅ㄡ泗轹暹痫ㄦ轭洵徙糸鲥痫箝糸镱狒糸礤镡矧ㄣ躜箫颦痫箦戽癌┅讳蜥筱犰镯鏖翳骁泔祜镯珏舡溽螂弪泔祜泔祜爱珐镯潋狩扉铄镯潋狩篝蜷铉ōǐ镯篝蜷铉箝钺礤暴ō暴钺礤┅讳蜥汨殪潋孱磲忮轭翳雉桢矧溴麸栳鲥翳泔蝌邈溟箴灬戾è痱弼痫轭铋飑祜镳骘轭糸礤箦聃孱沐珏舡糸礤洵轸屙扉篝镡戛骘翳孱ū椹滹戾舄è泺麸痖箦戽ㄩ翦憝珏舡轭翦蝾犰糸礤皓┅麒孱ㄡ钿痱弼痫轭ㄩ翦眢礤蜱邃痱弼痫轭皓戾è痱弼啉麸痖箦戽ㄩ翦憝珏舡轭翦蝾犰糸礤痱弼痫轭舂┅镯鏖翳犰痂爱镯鏖翳骁泔祜镯珏舡扉玷翦颦泔祜泔祜爱博镯潋狩蝈泗痱弼啉ō穿ō泺痱弼啉烘殪横铉戾候秕钿┅┅镯潋狩糸礤扉铄痫轭泺ǒ糸礤扉铄轸屙桢殓梏畅泔祜横泗轹瀛徙糸鲥唣矬后屐邈翦洵ㄡ钿箦戾泗邃箦戽ㄦ轭箦戾泗轱邃轸矧┅呼疱ㄩ翦憝珏舡豉疱皓呼轫ㄩ翦憝珏舡糸礤皓┅箦翩痱弼痫轭皓┅讳蜥糸汶殒轭翦蝠镬狒轱箦戾泗邃麒孱ㄡ钿铛礅弪ㄩ铘弪痫镡戛糸礤蝓戾邃轸矧┅换箫礤糸礤翳轶礤翳镤轶汜祆邃忮骘蝈翳糸礤蝓戾轶弼孱泸遽翦铒顼镤祜镳骘鲠骝镯磲霰糸礤蝓戾邃轸矧┅ㄧ弭骈蝮舡糸礤镡戛麸黹ㄧ弭镡戥漉镡戛霾糸礤蝓戾邃轸矧┅怡铛礅弪铛礅弪ㄩ铘弪痫镡戛滹戾è鲠麸痖箦戽鲠飑┅镯鏖翳骁泔祜镯珏舡溽螂弪泔祜泔祜爱暴镯潋狩扉铄鲠ō博鲠ǐ博┅┅┅ㄤ彐礤翳镤镯潋狩糸礤扉铄痫轭ㄣ泫蜥溟躞泔脲徙糸鲥箦戾泗邃豉疱糸礤戾è扉玷舡泔镯珏舡扉玷翦颦泔祜泔爱博┅麒孱ㄥ聃犰豉疱喉狍翦颟镯鏖翳骁泔祜镯磲脲泔祜爱爱爱供镯潋狩扉铄泺泺蛋镯潋狩汩蜚戾泺泫ǐ蜥溟躞烘殪铋飑┅讳蜥汩蜚戾麒孱徙糸鲥惑屐邈翦栝玷扉玷镯鏖翳骁泔祜泔镯潋狩汩蜚戾泺泫ǐ蜥溟躞暴烘殪舂┅镯鏖翳骁泔祜ㄩ箦戾泗邃镯溴姝泔祜轰狎氕蝈洎扉玷舡泔飑镯潋狩汩蜚戾泺泫ㄩ箦戾泗邃ū蜥溟躞蜥溟躞烘殪ㄩ糸礤铋飑┅┅换换换换换换换换换换换换换换换换换换换陨团土宜乓遗塘耘团匀夏换换换换换换换换换换换换换换换换换换换换辉赏土宜乓礤翳镤麸蝈溴骈铄怡篚忏灬篌弩ㄤ彐礤翳镤珏舡糸礤洵镡赍泗蟓骘颦珧徜踽翦洵鲩鬻è箦戽镯糸礤扉铄鲩鬻┅或弭躜铙扉篝镦糸礤洵镡赍泗麸蝈趄殄鲥翳彘磲螂弪扉篝ㄥ溟麸颦珏舡糸礤箦聃孱沐ㄣ镱翎轭弪邃轸矧ㄥ溟麸箦戽┅ㄩ箦戽┅┅辉赏土宜乓礤翳镤麸蝈溴骈铄怡篚忏灬篌弩ㄤ彐礤翳镤箦戾泗屐屙孱趔狒糸礤è箦戽镯糸礤扉铄鲩鬻磲螂弪糸礤惑屐邈趔翳屐屙孱趔鏖翳筢礤糸礤翳犷翳磲螂弪糸礤戾舄è邃轸矧ㄥ溟麸箦戽┅镡ㄥ溟麸颦珏舡糸礤箦聃孱沐邃轸矧ㄩ箦戽┅┅戾è痫轭舡屮轶趔狒糸礤镡磲螂弪糸礤┅麒孱箦戾泗糸礤扉铄箦戽舂ㄩ铒ㄦ轭箦戾泗轱邃轸矧┅箦翩箦戾泗轱邃轸矧ㄡ痧孱扉篝皓箦戾泗轱邃轸矧┅┅躔溽翦麸邃轸矧邃轸矧箦戽┅辉赏土宜乓ㄤ彐礤翳镤沆遽颦邃轸矧箦戾泗轱è箦戽糸礤扉铄邃轸矧┅箦舡箦戾泗轱箦戽铋飑箦舡箦戾泗邃糸礤扉铄箦戽铋飑换换换换换换换换换换换换换换换换换换换换换胖盼杂遗塘耘团匀夏换换换换换换换换换换换换换换换换换换换换换ㄤ彐礤翳镤糸礤洵轸屙轭溴狒糸礤è箦戽糸礤扉铄邃轸矧疳铄镯糸礤扉铄鲩鬻糸礤戾舄è镡ㄥ溟麸颦珏舡糸礤箦聃孱沐箦戽ㄩ疳铄飑┅糸礤糸礤箦聃孱沐珏舡轭翦蝾犰糸礤镡戛┅ㄩ戾铉翳糸礤螬癌戾舄è溴祠ǒ糸礤扉铄轸屙桢殓梏博糸礤痫麸痖疳铄糸礤┅痫ㄦ轭洵痫箝糸镱狒糸礤镡糸礤┅舯铘ㄩ冀痫癌ū痫螬糸礤螬痫蟊麸痖疳铄舯┅舨矧铘痫糸礤螬舯┅痫蟛麸痖疳铄舨┅ㄩㄡ钿痫癌ō糸礤痫痫蟊溴祠岍ū痫螬ㄩㄡ钿痫戾铉翳糸礤螬ō痫蟛糸礤痫螬溴祠岍痫铋飑┅铋飑┅ㄤ彐礤翳镤糸礤洵轸屙狒糸礤è箦戽糸礤扉铄邃轸矧疳铄镯糸礤扉铄鲩鬻糸礤戾舄è镡ㄥ溟麸颦珏舡糸礤箦聃孱沐箦戽ㄩ疳铄飑┅痫糸礤洵轸屙轭溴狒糸礤箦戽疳铄糸礤┅ㄩ痫铘痫糸礤箦聃孱沐珏舡糸礤洵轸屙扉篝镡戛铋飑┅ㄤ彐礤翳镤矧溴颦痫轭趔怡糸礤è箦戽糸礤扉铄邃轸矧┅祜镳骘綮轭糸礤扉铄鲩鬻箦戽滹戾è镡ㄥ溟麸颦珏舡糸礤箦聃孱沐箦戽ㄩ綮雯┅蝈矧溴颦麴镩铘镡戛┅ㄤ彐礤翳镤徜洵痫轭舡狒糸礤è箦戽糸礤扉铄邃轸矧糸礤殇戾舄è镡ㄥ溟麸颦珏舡糸礤箦聃孱沐箦戽殇┅痫轭糸礤箦聃孱沐磲脲糸礤洵轸屙狒镡蝻躅糸礤┅┅糸礤箦聃孱沐轭箦螋糸礤洵轸屙犷洵躔溽翦镡痫轭舂┅ㄤ彐礤翳镤趄犷箪狒瀛箦戾泗轱è箦戽糸礤扉铄邃轸矧漪麒孱箦戾泗轱箦戽祜镳骘綮轭糸礤扉铄鲩鬻箦戽滹戾舄è镡ㄥ溟麸颦珏舡糸礤箦聃孱沐箦戽ㄩ綮雯┅痫轭趔ㄦ殪翦颦痫轭趔骘颦镡镡箦戾泗轱箦戽┅┅麒孱痫轭趔翦眇矧犰趄犷箪狒瀛痫轭趔镡痫轭趔漪┅┅矧溴颦痫轭趔怡糸礤箦戽┅ㄤ彐礤翳镤箦舡箦戾泗轱瞽狍磲篝弪è箦戽糸礤扉铄邃轸矧殇麒孱箦戾泗轱箦戽戾è镡ㄥ溟麸颦珏舡糸礤箦聃孱沐箦戽殇┅ㄣ镱è骈钿箦戾泗轱箦戽┅箦舡犰飙痫轭趔狍磲篝弪镡戛祜镳骘痫轭轭箦戾泗轱箦戽滹ㄩ翦憝箦舡豉疱痫轭ㄩㄥ耢喉狍翦ㄩ翦憝珏舡豉疱痫轭舂ㄩ翦憝珏舡糸礤痫轭舂喉狍翦颟┅躔溽翦糸礤豉疱蟓骝镯麴镩铘扉篝镡戛┅┅ㄤ彐礤翳镤珏舡箦戾泗邃轭溟沐蟓骘颦鲩鬻è箦戽镯糸礤扉铄鲩鬻┅戾è镡ㄥ溟麸颦珏舡糸礤箦聃孱沐ㄥ溟麸箦戽ㄩ箦戽┅┅ㄧ弭轭溟沐蟓骝镯痫轭趔镡箦戾泗轱ㄥ溟麸箦戽┅┅ㄤ彐礤翳镤珏舡箦戾泗邃痫轭趔骘颦鲩鬻è箦戽镯糸礤扉铄鲩鬻┅戾è镡ㄥ溟麸颦珏舡糸礤箦聃孱沐ㄥ溟麸箦戽ㄩ箦戽┅┅ㄦ殪翦颦痫轭趔骘颦镡镡箦戾泗轱ㄥ溟麸箦戽┅┅ㄤ彐礤翳镤珏舡箦戾泗轱瞽狍鲩鬻蟓轭溟沐蟓扉篝è箦戽糸礤扉铄邃轸矧┅或弭躜扉篝鏖翳钼镦鲩鬻屐屙孱趔遽汨狍骘祆秣蠛鲩鬻殇ㄩ钿殂弩轭溟沐蟊轭溟沐箢┅祜镳骘綮轭糸礤扉铄鲩鬻箦戽泔祆邈扉篝ㄩ綮雯ㄧ弭箦戾泗邃轭溟沐蟓骘颦鲩鬻綮雯┅ㄤ彐礤翳镤珏舡镡戥骘颦痫轭è箦戽糸礤扉铄邃轸矧痫轭舂或弭躜铙翳镡赍泗泔铘衢铉翳痫轭祜镳骘綮轭糸礤扉铄鲩鬻箦戽麒孱痫箝糸镱痫轭糸礤箦聃孱沐珏舡糸礤洵轸屙扉篝ㄥ溟麸颦珏舡糸礤箦聃孱沐箦戽ㄩ綮雯┅蝈趱蝾ㄥ溟麸颦珏舡糸礤箦聃孱沐箦戽ㄩ綮雯┅ㄤ彐礤翳镤盹鲥糸礤痫轭舡徙糸镱è鲩鬻镯糸礤扉铄鲩鬻邃轸矧矧殓痫轭痫箝糸镱戾舄è糸礤痖麸鲩鬻镯痫轭舡痫箝糸镱┅┅镯轭轸翦眇珧狃栝泱盹糸镱鲩鬻痫箝糸镱铋喉轭盹鲥喉雉轱＇灬礅溽鲩鬻痫螬戾舄è繇疬糸礤痖屐麸糸礤鲩鬻镯痫轭舡痫螬┅ㄤ蝻躅ō繇疬糸礤糸礤┅箦戾泗邃痫轭舡糸礤ㄩ翦憝珏舡轭翦蝾犰糸礤矧殓痫轭舂┅箦舡糸礤溟箴灬邃轸矧繇疬糸礤麒孱箦戾泗轱邃轸矧戾舄è铄鳝漪ㄩ箢狃麸珧殇邃轸矧ㄡ溽痿漪骘颦珧殇犷洵磲螂弪糸礤蝓戾邃轸矧箦戾泗邃痫轭舡糸礤漪漪┅麒孱铒ㄥ聃犰铄鳝漪癌箦翩糸礤ǐ糸礤铄鳝漪┅趄犷箪狒瀛箦戾泗轱邃轸矧铄鳝漪箦舡沲蝮矧糸礤邃轸矧ㄩ翦憝珏舡轭翦蝾犰糸礤矧殓痫轭舂┅镯轭鲠扉溽翦鲩鬻鲩鬻麒孱ㄥ聃犰喉狍翦ㄩ翦憝珏舡豉疱矧殓痫轭舂镯轭鲠扉溽翦鲩鬻糸礤蝓戾邃轸矧┅躔溽翦麸邃轸矧ㄣ镱翎轭弪邃轸矧邃轸矧邃轸矧┅┅┅换换换换换换换换换换换换换胖盼杂换换换换换换换换换换换换换ㄤ彐礤翳镤犰祆秣轭箦螋痫轭舡骝镯糸礤扉铄è箦戽贤配轸矧┅舂ㄤ彐礤翳镤镯鲩鬻沆殂氕栳钿戾è箦戽镯糸礤扉铄鲩鬻痫箝糸镱戾舄è糸礤扉铄邃轸矧ㄥ溟麸箦戽┅糸礤痖麸箦戽镯痫轭舡痫箝糸镱┅痫轭糸礤洵轸屙狒糸礤糸礤扉铄邃轸矧箦戽糸礤┅会滗痫轭殒徜脲滹黝犷痫轭铒屮轶糸铉麒孱ㄡ钿镯徜洵脲滹黝ㄡ祆祜鳝轭箦螋痫轭舡骝镯糸礤扉铄ㄣ镱翎轭弪邃轸矧糸礤扉铄邃轸矧┅铒痫轭舂戾è痫ㄡ滗痫轭舡狒糸礤糸礤扉铄邃轸矧糸礤ㄩ箦戽┅镡ㄥ溟麸颦珏舡糸礤箦聃孱沐糸礤扉铄邃轸矧ㄩ箦戽┅┅箦翩痫轭ㄧ弭铘璀痫轭镡痫螬┅霍轫屐轭弩鲩鬻箦戾泗轱ㄣ镱è镯箬殒舡脲皓戾è箦飙骘颦鲩鬻ㄧ弭箦戾泗邃轭溟沐蟓骘颦鲩鬻箦戽┅ㄩㄡ钿箦戾泗邃箦戽铒箦飙骘颦鲩鬻铒痫轭舂箦戾泗糸礤扉铄箦戽铋飑箦戾泗糸礤扉铄箦戽舂┅箦舡箦戾泗邃糸礤扉铄糸礤扉铄邃轸矧扉篝ㄩ箦戽┅┅箦舡箦戾泗轱糸礤扉铄邃轸矧痫轭舂火镩铘箦戾泗轱ㄩ痫轭痱镧盹鲥糸礤痫轭舡徙糸镱箦戽糸礤扉铄邃轸矧痫轭痫箝糸镱箦舡沲蝮矧糸礤糸礤扉铄邃轸矧矧ㄡ钿痫轭ㄩ翦憝珏舡糸礤痫轭舂糸礤┅躔溽翦麸邃轸矧ㄣ镱翎轭弪邃轸矧糸礤扉铄邃轸矧糸礤扉铄邃轸矧┅矧ㄣ犰飙铄舡礤翳镤痱镧箦舡沲蝮矧糸礤糸礤扉铄邃轸矧矧ㄡ钿痫轭ㄩ翦憝珏舡糸礤痫轭舂糸礤┅ㄤ蜥绛盹鲥沲蝮矧箦戽痫箝糸镱┅┅箦舡糸礤溟箴灬糸礤扉铄邃轸矧ㄩ痫轭ㄩ翦憝珏舡糸礤痫轭舂糸礤┅痫轭舂花溴骓弭栾镯鲩鬻盹躞瀛盹糸镱栳钿戾è箦戽镯糸礤扉铄鲩鬻痫箝糸镱戾è邃轸矧ㄥ溟麸箦戽┅糸礤痖麸箦戽镯痫轭舡痫箝糸镱┅┅箦舡糸礤溟箴灬邃轸矧糸礤┅ㄤ彐礤翳镤邃轸矧脲徙糸镱è邃轸矧糸礤扉铄邃轸矧脲ㄣ狍脲ê镯脲溴戾翦磲疸狎＇灬礅溽糸礤扉铄殇ㄥ溟麸颦溴戾翦泔铘孱趔骝镯糸礤扉铄ㄣ镱翎轭弪邃轸矧邃轸矧糸礤扉铄殇箦戾泗轱邃轸矧┅镯轭鲠扉溽翦鲩鬻铘糸礤扉铄殇糸礤扉铄鲩鬻邃轸矧┅┅ㄧ弭箦戾泗邃糸礤扉铄邃轸矧┅箦翩箦戾泗轱邃轸矧铋飑躔溽翦麸邃轸矧ㄣ镱翎轭弪邃轸矧邃轸矧邃轸矧舂ǎ荃磲疸狎＇灬礅溽糸礤扉铄殇箢狃犰飙痫轭趔麸珧殇邃轸矧糸礤扉铄殇┅ㄧ弭箦戾泗邃糸礤扉铄邃轸矧┅躔溽翦麸邃轸矧ㄣ镱翎轭弪邃轸矧邃轸矧邃轸矧舂ǎ茼磲疸狎＇灬礅溽糸礤扉铄殇箦舡箦戾泗轱瞽狍磲篝弪邃轸矧糸礤扉铄殇镯轭鲠扉溽翦鲩鬻铘糸礤扉铄殇糸礤扉铄鲩鬻邃轸矧┅ㄧ弭箦戾泗邃糸礤扉铄邃轸矧┅躔溽翦麸邃轸矧ㄣ镱翎轭弪邃轸矧邃轸矧邃轸矧镯轭鲠扉溽翦鲩鬻糸礤蝓戾邃轸矧┅舂雉桢蝼轶铋飑┅ㄤ彐礤翳镤镯鲩鬻脲栳钿戾è箦戽镯糸礤扉铄鲩鬻脲矧ㄥ溟麸颦脲徙糸镱ㄥ溟麸箦戽脲ㄣ犰飙铄舡礤翳镤┅换骄麸鏖钿秣犷痨狴邃轸矧黹轭ㄤ彐礤翳镤邃轸矧痨狴è箦戽糸礤扉铄邃轸矧┅ㄥ溟麸颦痨狴ㄣ镱翎轭弪邃轸矧箦戽┅ㄤ彐礤翳镤邃轸矧疳躞è箦戽糸礤扉铄邃轸矧┅ㄥ溟麸颦疳躞ㄣ镱翎轭弪邃轸矧箦戽┅ㄤ彐礤翳镤邃轸矧篝镳è箦戽糸礤扉铄邃轸矧┅ㄥ溟麸颦篝镳ㄣ镱翎轭弪邃轸矧箦戽┅ㄤ彐礤翳镤邃轸矧箦舡轭翦蝣犰è箦戽糸礤扉铄邃轸矧轭翦蝣犰ㄥ溟麸颦箦舡轭翦蝣犰ㄣ镱翎轭弪邃轸矧箦戽轭翦蝣犰┅